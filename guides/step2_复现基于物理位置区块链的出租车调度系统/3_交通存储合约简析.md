### 合约代码简析
> 前人的合约代码经过笔者的修整可以在新的环境下编译，详见`doc/`目录下的[StoreTraffic合约文件改动记录](../../doc/StoreTraffic%E5%90%88%E7%BA%A6%E6%96%87%E4%BB%B6%E6%94%B9%E5%8A%A8%E8%AE%B0%E5%BD%95.md)。修整后的合约在[这里](../../src/contract_on_system/StoreTraffic.sol)。
#### 状态变量说明
- 一条导航路径`oneRote`包括路径点队列`routeCoords`和导航路径的时间消耗`routeCost`。一条导航路径和车辆id绑定`routes`
- 一台车辆`vehicle`由车辆id`vehicleId`、车辆位置`position`和车辆状态`status`组成。每台车辆和车辆id绑定`vehicles`。车辆序号和车辆id绑定`vehiclesList`。当前存储的车辆数`vehiclesLength`
- 一个乘客`passenger`包括乘客id`passengerId`、乘客当前位置`position`、乘客出发地`start`、乘客目的地`end`和乘客状态`status`。一个乘客和乘客id绑定`passengers`。乘客序号和乘客id绑定`passengerList`。当前存储的乘客数`passengersLength`
#### 函数说明
> 可以在`reference/investigation-cjzhuang2020/cjz_underg_2021_09`以及`reference/GraduationProject-main/code/前端/app2.0(driver2.0)`目录下搜索相应的函数查看其调用时机。完整的系统运行流程请参考本部分的`6_出租车调度系统概述`。
- `confirmPay`乘客确认支付：
    ```
    function confirmPay(bytes32 vehicleId) public {
        //触发乘客付款事件，司机监听到之后进行设置车辆为空闲状态等操作
        emit payEvent(vehicleId);
    }
    ```
- `confirmBoard`乘客确认上车：
    ```
    function confirmBoard(bytes32 vehicleId) public {
        //触发乘客上车事件，司机监听到之后可以进行路径规划等操作
        emit boardEvent(vehicleId);
	}
    ```
- `storeRoutes`司机将车辆的本次导航路径存储到区块链，在确认接客并导航后调用：
    ```
    function storeRoutes(uint256 cost, bytes32 vehicleId, bytes32 passengerId, bytes32[] memory route) public {
        routes[vehicleId].routeCoords = route;      //storge <-- memory 值拷贝
		routes[vehicleId].routeCost = cost;
        //触发路径确认事件，乘客监听到之后可以进行路径绘制等操作
        emit routeEvent(passengerId);
    }
    ```
- `getRoutes`获取某车辆的导航路线可以用于绘制地图等操作：
    ```
    function getRoutes(bytes32 vehicleId) public view returns(bytes32[] memory route, uint256 cost){
        route = routes[vehicleId].routeCoords;
		cost = routes[vehicleId].routeCost;
    }
    ```
- `getVehicleStatus`根据车辆id获取车辆当前状态，多用于测试：
    ```
    function getVehicleStatus(bytes32 vehicleId) public view returns (uint256){
        uint256 status = vehicles[vehicleId].status;
        return status;
    }
    ```
- `setVehicle`设置车辆位置函数，乘客付款下车之后司机调用该函数设置一下车辆位置：
    ```
    function setVehicle(bytes32 vehicleId, bytes32 vehicleGeohash) public {
        vehicles[vehicleId].position = vehicleGeohash;
    }
    ```
- `setVehiclesStatus`设置车辆状态函数，乘客匹配车辆成功时乘客调用：
    ```
    function setVehicleStatus(bytes32 vehicleId, bytes32 passengerId, bytes32 passengerGeohash) public {
        require(vehicles[vehicleId].status == 0, "Vehicle busy");
        emit Myevent(           //触发匹配成功的事件，司机监听到之后绘制一下乘客上车地，然后选择接客or不接客
            vehicleId,
            passengerId,
            passengerGeohash
        );
        vehicles[vehicleId].status = 1;
    }
    ```
- `setVehicleStatusEmpty`设置车辆为空闲状态，乘客付款后司机调用：
    ```
	function setVehicleStatusEmpty(bytes32 vehicleId) public {
        require(vehicles[vehicleId].status == 1, "Vehicle is not busy");
        vehicles[vehicleId].status = 0;
    }
    ```
- `setRejectVehicleStatus`车辆因拒绝乘客而进入的空闲：
    ```
    function setRejectVehicleStatus(bytes32 vehicleId, bytes32 passengerId) public {
        require(vehicles[vehicleId].status == 1, "Vehicle is not busy");
        vehicles[vehicleId].status = 0;
        //触发拒绝接客事件，乘客监听到后打印一些信息
        emit rejectEvent(passengerId);
    }
    ```
- `setPassengerPosition`设置乘客位置，好像并未用到该函数：
    ```
    function setPassengerPosition(
        bytes32 passengerId,
        bytes32 passengerGeohash
    ) public {
        passengers[passengerId].position = passengerGeohash;
    }
    ```
- `setPassengerDemand`设置乘客的初始位置和目的地，乘客端在初始化乘客之后调用：
    ```
    function setPassengerDemand(
        bytes32 passengerId,
        bytes32 startGeohash,
        bytes32 endGeohash
    ) public {
        passengers[passengerId].start = startGeohash;
        passengers[passengerId].end = endGeohash;
    }
    ```
- `getPassengerPos`获取乘客当前位置，好像并未用到该函数：
    ```
    function getPassengerPos(bytes32 passengerId) public view returns (bytes32 position){
        position = passengers[passengerId].position;
    } 
    ```
- `getPassengerEnd`获取乘客目的地并把乘客标记为繁忙，车辆接到乘客后导航前司机调用：
    ```
    function getPassengerEnd(bytes32 passengerId) public returns (bytes32 end){
        end = passengers[passengerId].end;
        passengers[passengerId].status = 1;
    }
    ```
- `getVehicle`乘客发起的在所有车辆中进行的乘车匹配。根据传入的乘客位置匹配距离其最近的车辆，返回车辆位置和车辆id：
    ```
    function getVehicle(bytes32 passengerGeohash) public view returns (bytes32, bytes32){
        require(vehiclesLength > 0, "No vehicle in system!");
        bytes32 position = vehicles[vehiclesList[0]].position;
        uint256 index;
        for(uint256 i = 0; i < vehiclesLength; i++){
            if(manhattan(passengerGeohash,vehicles[vehiclesList[i]].position) < manhattan(passengerGeohash,position)){
                if(vehicles[vehiclesList[i]].status == 0){
                    position = vehicles[vehiclesList[i]].position;
                    index = i;
                }
            }
        }
        return (position,vehiclesList[index]);
    }
    ```
- `getVehicleByRegion`乘客发起的在某一特定区域内进行的乘车匹配。根据传入的乘客位置匹配队列中离乘客最近的车辆，返回车辆位置和车辆id：
    ```
    function getVehicleByRegion(bytes32 passengerGeohash, bytes32[] memory regionVehicles) public view returns (bytes32, bytes32){
        require(regionVehicles.length > 0, "No vehicle in this area");
        bytes32 position = vehicles[regionVehicles[0]].position;
        uint256 index;
        for(uint256 i = 0; i < regionVehicles.length; i++){
            if(manhattan(passengerGeohash,vehicles[regionVehicles[i]].position) < manhattan(passengerGeohash,position)){
                if(vehicles[regionVehicles[i]].status == 0){
                    position = vehicles[regionVehicles[i]].position;
                    index = i;
                }
            }
        }
        return (position,regionVehicles[index]);
    }
    ```
### 本部分悬而未决的问题
- 个人认为删除车辆和初始化车辆的代码有bug，只是将value设置成了默认值，key还在。而且把车辆list长度减了最后一个车可能索引不到了。
    ```
    //初始化车辆函数：根据车辆的id和geohash位置在区块链上初始化一个车辆
    function initVehicle(
        bytes32 vehicleId,
        bytes32 geohash
    ) public {
        vehicles[vehicleId].vehicleId = vehicleId;
        vehicles[vehicleId].position = geohash;
        vehicles[vehicleId].status = 0;
        vehiclesList[vehiclesLength] = vehicleId;
        vehiclesLength = vehiclesLength + 1;
    }
    //删除车辆函数：如果存在这个id的车辆则删除它（疑问：vehiclesList里的信息不用删除吗？）
    function deleteVehicle(bytes32 vehicleId) public {
        if(vehicleId == vehicles[vehicleId].vehicleId){
            delete vehicles[vehicleId];
            vehiclesLength = vehiclesLength - 1;
        }
    }
    ```